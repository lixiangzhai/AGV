import java.text.SimpleDateFormat

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id('com.github.sgtsilvio.gradle.android-retrofix') version '0.5.0'
    id 'org.jetbrains.kotlin.plugin.serialization'
}

android {
    namespace 'com.reeman.agv'
    compileSdk 33

    defaultConfig {
        applicationId "com.reeman.agv"
        minSdk 21
        targetSdk 33
        versionCode 14
        versionName "1.9.0"
        flavorDimensions "default"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField 'String', 'APP_LOG_DIR', '"agv_log"'
        buildConfigField 'String', 'CRASH_LOG_DIR', '"agv_carsh_log"'
        buildConfigField 'Boolean', 'APP_FORCE_USE_ZH', 'false'
        buildConfigField 'String', 'ELEVATOR_URL', '""'

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        board_other {
            dimension "default"
            buildConfigField 'Boolean', 'IS3128', 'false'
        }

        board_3128 {
            dimension "default"
            applicationIdSuffix ".b3128"
            versionNameSuffix "_3128"
            buildConfigField 'Boolean', 'IS3128', 'true'
        }

    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        resources.excludes.add("META-INF/*")
        resources.excludes.add("META-INF/io.netty.versions.properties")
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def versionName = variant.versionName
            outputFileName = "AGV_v${versionName}_${defaultConfig.applicationId}-${getCurrentTime()}.apk"
        }
    }

    /*packagingOptions {
        resources {
            excludes += ['META-INF/INDEX.LIST', 'META-INF/io.netty.versions.properties']
        }
    }*/

}

configurations.configureEach {
    resolutionStrategy {
        force "org.jetbrains.kotlin:kotlin-stdlib:1.8.0"
        force "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0"
    }
}


def static getCurrentTime() {
    SimpleDateFormat formatter = new SimpleDateFormat("yyyyMMddHHmm")
    Date curDate = new Date(System.currentTimeMillis())
    return formatter.format(curDate)
}


dependencies {
    implementation files('libs\\indicatorseekbar-2.1.2.aar')

    implementation project(path: ':lib_commons')
    implementation project(path: ':lib_ros')
    implementation project(path: ':lib_dao')
    implementation project(path: ':lib_calling')
    implementation project(path: ':lib_elevator_control')
    implementation project(path: ':pickerview')
    implementation project(path: ':lib_points')
    implementation project(path: ':lib_dispatch')

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    retrofix('net.sourceforge.streamsupport:android-retrostreams:1.7.4')
    retrofix('net.sourceforge.streamsupport:android-retrofuture:1.7.4')

    implementation 'com.iqiyi.xcrash:xcrash-android-lib:3.0.0'

    implementation 'com.github.li-xiaojun:EasyAdapter:1.2.5'

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation 'com.github.ybq:Android-SpinKit:1.4.0'

    implementation 'com.kyleduo.switchbutton:library:2.1.0'

    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.0.0"

    implementation 'com.tencent:mmkv:1.3.2'

}